#Область СлужебныйПрограммныйИнтерфейс

Функция ИмяНабора() Экспорт

	Возврат "Движение товаров";

КонецФункции

Процедура ИсполняемыеСценарии() Экспорт
    
   ЮТТесты.ДобавитьТестовыйНабор(ИмяНабора(), "ИльинАВ, Остатки товаров, Взаиморасчеты, Приход, Расход")
   	.ДобавитьТест("ТестСозданияДокументов", "Проверяет, что документы записываются", "Позитив").ВТранзакции()
	.ДобавитьТест("ТестПроведенияДокумента", "Проверяет, что документы проводятся (есть движения)", "Позитив").ВТранзакции()
	.ДобавитьТест("ТестОстатковПоРегистрамНакопления", "Проверяет, что остатки по регистрам накопления выходят в ноль", "Позитив").ВТранзакции()
	;

КонецПроцедуры

Процедура ТестСозданияДокументов() Экспорт
	
	// 1. Подготовка параметров и генерация данных для выполнения функции.
	Контекст = КонтекстТеста();
	
	// 2. Выполнение функции.
	
	// 3. Проверка результата.
	ЮТест.ОжидаетЧто(Контекст.ПриходТовара)
		.Метод("Записать")
		.НеВыбрасываетИсключение()
		.Свойство("Ссылка").Заполнено("Документ ПриходТовара успешно записан");

	ЮТест.ОжидаетЧто(Контекст.РасходТовара)
		.Метод("Записать")
		.НеВыбрасываетИсключение()
		.Свойство("Ссылка").Заполнено("Документ РасходТовара успешно записан");
		
	ЮТест.ОжидаетЧто(Контекст.Оплата)
		.Метод("Записать")
		.НеВыбрасываетИсключение()
		.ИмеетТип("ДокументОбъект.Оплата")
		.Свойство("Ссылка").Заполнено("Документ Оплата успешно записан");
		
	ЮТест.ОжидаетЧто(Контекст.ПоступлениеДенег)
		.Метод("Записать")
		.НеВыбрасываетИсключение()
		.Свойство("Ссылка").Заполнено("Документ ПоступлениеДенег успешно записан");
		
КонецПроцедуры 

Процедура ТестПроведенияДокумента() Экспорт
	
	// 1. Подготовка параметров и генерация данных для выполнения функции.
	Контекст = КонтекстТеста();
	
	// 3. Проверка результата.
	ЮТест.ОжидаетЧто(Контекст.ПриходТовара)
		.Метод("Записать").Параметр(РежимЗаписиДокумента.Проведение)
		.НеВыбрасываетИсключение()
		.Свойство("Проведен").ЭтоИстина("Документ имеет признак Проведен")
		.Свойство("Движения.Взаиморасчеты").Заполнено("Есть движение по РН Взаиморасчеты")
		.Свойство("Движения.ТоварныеЗапасы").Заполнено("Есть движение по РН ТоварныеЗапасы");
		
	ЮТест.ОжидаетЧто(Контекст.РасходТовара)
		.Метод("Записать").Параметр(РежимЗаписиДокумента.Проведение)
		.НеВыбрасываетИсключение()
		.Свойство("Проведен").ЭтоИстина("Документ имеет признак Проведен")
		.Свойство("Движения.Взаиморасчеты").Заполнено("Есть движение по РН Взаиморасчеты")
		.Свойство("Движения.ТоварныеЗапасы").Заполнено("Есть движение по РН ТоварныеЗапасы");
		
	ЮТест.ОжидаетЧто(Контекст.Оплата)
		.Метод("Записать").Параметр(РежимЗаписиДокумента.Проведение)
		.НеВыбрасываетИсключение()
		.ИмеетТип("ДокументОбъект.Оплата")
		.Свойство("Проведен").ЭтоИстина("Документ имеет признак Проведен")
		.Свойство("Движения.Взаиморасчеты").Заполнено("Есть движение по РН Взаиморасчеты");
		
	ЮТест.ОжидаетЧто(Контекст.ПоступлениеДенег)
		.Метод("Записать").Параметр(РежимЗаписиДокумента.Проведение)
		.НеВыбрасываетИсключение()
		.Свойство("Проведен").ЭтоИстина("Документ имеет признак Проведен")
		.Свойство("Движения.Взаиморасчеты").Заполнено("Есть движение по РН Взаиморасчеты");
		
КонецПроцедуры 

Процедура ТестОстатковПоРегистрамНакопления() Экспорт
	
	// 1. Подготовка параметров и генерация данных для выполнения функции.
	Контекст = КонтекстТеста();

	ОписаниеЗапросаТоварныеЗапасы = ЮТЗапросы.ОписаниеЗапроса();
	ОписаниеЗапросаТоварныеЗапасы.ИмяТаблицы = СтрШаблон("РегистрНакопления.ТоварныеЗапасы.Остатки(&Период, Склад = &Склад И Товар = &Товар)");
	ОписаниеЗапросаТоварныеЗапасы.ВыбираемыеПоля.Добавить("КоличествоОстаток");
	ОписаниеЗапросаТоварныеЗапасы.ЗначенияПараметров.Вставить("Период", КонецДня(ТекущаяДатаСеанса()));
	ОписаниеЗапросаТоварныеЗапасы.ЗначенияПараметров.Вставить("Склад", Контекст.Склад);
	ОписаниеЗапросаТоварныеЗапасы.ЗначенияПараметров.Вставить("Товар", Контекст.Номенклатура);

	ОписаниеЗапросаВзаиморасчеты = ЮТЗапросы.ОписаниеЗапроса();
	ОписаниеЗапросаВзаиморасчеты.ИмяТаблицы = СтрШаблон("РегистрНакопления.Взаиморасчеты.Остатки(&Период, Контрагент = &Контрагент)");
	ОписаниеЗапросаВзаиморасчеты.ВыбираемыеПоля.Добавить("СуммаОстаток");
	ОписаниеЗапросаВзаиморасчеты.ЗначенияПараметров.Вставить("Период", КонецДня(ТекущаяДатаСеанса()));
	ОписаниеЗапросаВзаиморасчеты.ЗначенияПараметров.Вставить("Контрагент", Контекст.Поставщик);
	
	// 2. Выполнение функции.
	РезультатЗапросаТоварныеЗапасы = ЮТЗапросы.РезультатЗапроса(ОписаниеЗапросаТоварныеЗапасы);
	
	ОписаниеЗапросаВзаиморасчеты.ЗначенияПараметров.Вставить("Контрагент", Контекст.Поставщик);
	РезультатЗапросаВзаиморасчетыПоставщик = ЮТЗапросы.РезультатЗапроса(ОписаниеЗапросаВзаиморасчеты);
	
	ОписаниеЗапросаВзаиморасчеты.ЗначенияПараметров.Вставить("Контрагент", Контекст.Покупатель);
	РезультатЗапросаВзаиморасчетыПокупатель = ЮТЗапросы.РезультатЗапроса(ОписаниеЗапросаВзаиморасчеты);

	// 3. Проверка результата.
	ЮТест.ОжидаетЧто(РезультатЗапросаТоварныеЗапасы[0]["КоличествоОстаток"])
		.ИмеетТип("Число")
		.Равно(0, "Количество остатка равно 0 (ноль)");
		
	ЮТест.ОжидаетЧто(РезультатЗапросаВзаиморасчетыПоставщик[0]["СуммаОстаток"])
		.ИмеетТип("Число")
		.Равно(0, "Остаток взаиморасчетов с поставщиком равен 0 (ноль)");

	ЮТест.ОжидаетЧто(РезультатЗапросаВзаиморасчетыПокупатель[0]["СуммаОстаток"])
		.ИмеетТип("Число")
		.Равно(0, "Остаток взаиморасчетов с покупателем равен 0 (ноль)");
		
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ГенерацияДанных

Функция КонтекстТеста()
	
	Контекст = ЮТест.КонтекстТеста();
	Контекст.Вставить("Дата", ТекущаяДатаСеанса());
	Контекст.Вставить("Валюта", НовыйЭлементСправочника_Валюты());
	
	Контекст.Вставить("Организация", ЮТТестовыеДанные.СоздатьЭлемент("Справочник.Организации", "Организация тест"));
	Контекст.Вставить("РасчетныйСчет", 
		ЮТТестовыеДанные.СоздатьЭлемент("Справочник.РасчетныеСчета", "р/с Организации тест", Новый Структура("Владелец", Контекст.Организация)));
	
	Контекст.Вставить("Поставщик", ЮТТестовыеДанные.СоздатьЭлемент("Справочник.Контрагенты", "Поставщик тест"));
	Контекст.Вставить("РасчетныйСчетПоставщика", 
		ЮТТестовыеДанные.СоздатьЭлемент("Справочник.РасчетныеСчетаКонтрагентов", "р/с Поставщика", Новый Структура("Владелец", Контекст.Поставщик)));
		
	Контекст.Вставить("Покупатель", ЮТТестовыеДанные.СоздатьЭлемент("Справочник.Контрагенты", "Покупатель тест"));
	Контекст.Вставить("РасчетныйСчетПокупателя", 
			ЮТТестовыеДанные.СоздатьЭлемент("Справочник.РасчетныеСчетаКонтрагентов", "р/с Покупателя", Новый Структура("Владелец", Контекст.Покупатель)));
			
	Контекст.Вставить("Склад", ЮТТестовыеДанные.СоздатьЭлемент("Справочник.Склады", "Склад тест"));
    Контекст.Вставить("Номенклатура", ЮТТестовыеДанные.СоздатьЭлемент("Справочник.Товары", "Товар тест", Новый Структура("Вид", Перечисления.ВидыТоваров.Товар)));
	Контекст.Вставить("Количество", 1);
	Контекст.Вставить("Сумма", 1000);

	Контекст.Вставить("ПриходТовара", НовыйДокумент_ПриходТовара());
	Контекст.Вставить("РасходТовара", НовыйДокумент_РасходТовара());
	Контекст.Вставить("Оплата", НовыйДокумент_Оплата());
	Контекст.Вставить("ПоступлениеДенег", НовыйДокумент_ПоступлениеДенег());
	
	Возврат Контекст;
	
КонецФункции

Функция НовыйДокумент_ПриходТовара()
	
	Контекст = ЮТест.КонтекстТеста();

	ДокументОбъект = ЮТест.Данные().КонструкторОбъекта("Документ.ПриходТовара")
	    .ФикцияОбязательныхПолей()
		.Установить("Дата", НачалоДня(Контекст.Дата))
		.Установить("Поставщик", Контекст.Поставщик)
	    .ТабличнаяЧасть("Товары")
	      .ДобавитьСтроку()
	        .Установить("Товар", Контекст.Номенклатура)
	        .Установить("Количество", Контекст.Количество) 
			.Установить("Цена", Контекст.Сумма)
			.Установить("Сумма", Контекст.Сумма)
		.Записать(Истина);

	Возврат ДокументОбъект;

КонецФункции

Функция НовыйДокумент_РасходТовара()

	Контекст = ЮТест.КонтекстТеста();
	
	ДокументОбъект = ЮТест.Данные().КонструкторОбъекта("Документ.РасходТовара")
	    .ФикцияОбязательныхПолей()
		.Установить("Дата", НачалоДня(Контекст.Дата) + 1)
		.Установить("Покупатель", Контекст.Покупатель)
	    .ТабличнаяЧасть("Товары")
	      .ДобавитьСтроку()
	        .Установить("Товар", Контекст.Номенклатура)
	        .Установить("Количество", Контекст.Количество)
			.Установить("Цена", Контекст.Сумма)
			.Установить("Сумма", Контекст.Сумма)
		.Записать(Истина);

	Возврат ДокументОбъект;

КонецФункции

Функция НовыйДокумент_Оплата()
	
	Контекст = ЮТест.КонтекстТеста();
	
	ДокументОбъект = ЮТест.Данные().КонструкторОбъекта("Документ.Оплата")
		.Установить("Дата", НачалоДня(Контекст.Дата) + 1)
		.Установить("Организация", Контекст.Организация)
		.Установить("РасчетныйСчет", Контекст.РасчетныйСчет)
		.Установить("Поставщик", Контекст.Поставщик)
		.Установить("РасчетныйСчетПоставщика", Контекст.РасчетныйСчетПоставщика)
		.Установить("Валюта", Контекст.Валюта)
		.Установить("Сумма", Контекст.Сумма)
		.ФикцияОбязательныхПолей()
		.Записать(Истина);

	Возврат ДокументОбъект;

КонецФункции

Функция НовыйДокумент_ПоступлениеДенег()

	Контекст = ЮТест.КонтекстТеста();

	ДокументОбъект = ЮТест.Данные().КонструкторОбъекта("Документ.ПоступлениеДенег")
		.Установить("Дата", НачалоДня(Контекст.Дата) + 2)
		.Установить("Организация", Контекст.Организация)
		.Установить("РасчетныйСчет", Контекст.РасчетныйСчет)
		.Установить("Покупатель", Контекст.Покупатель)
		.Установить("РасчетныйСчетПокупателя", Контекст.РасчетныйСчетПокупателя)
		.Установить("Валюта", Контекст.Валюта)
		.Установить("Сумма", Контекст.Сумма)
		.ФикцияОбязательныхПолей()
		.Записать(Истина);

	Возврат ДокументОбъект;

КонецФункции

Функция НовыйЭлементСправочника_Валюты(Наименование = "Рубли")
	
	//ЭлементСсылка = ЮТТестовыеДанные.Ссылка("Справочник.Валюты", Наименование);
	ЭлементСсылка = Справочники.Валюты.НайтиПоНаименованию(Наименование, Истина);
	
	Если Не ЗначениеЗаполнено(ЭлементСсылка) Тогда
		ЭлементСсылка = ЮТТестовыеДанные.СоздатьЭлемент("Справочник.Валюты", Наименование);
	КонецЕсли;

	Возврат ЭлементСсылка;
		
КонецФункции

#КонецОбласти

#КонецОбласти